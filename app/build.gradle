apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: "$rootDir/common-methods.gradle"
apply from: 'build-flavors.gradle'
apply plugin: 'io.fabric'
apply plugin: 'com.getkeepsafe.dexcount'

final userName = System.getProperty("user.name")
final credentialsFolder = getCredentialsFolder(userName)

final releaseKeystoreProperties = getKeystoreProperties(credentialsFolder + "release-keystore.properties")
final debugKeystoreProperties = getKeystoreProperties(credentialsFolder + "debug-keystore.properties")

repositories {
    maven {
        url 'https://maven.fabric.io/public'
    }
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId appId
        minSdkVersion 15
        targetSdkVersion 28
        versionCode appVersionCode.toInteger()
        versionName appVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [toothpick_registry_package_name: appId]
                includeCompileClasspath = true
            }
        }
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
        configureApkName()
    }
    signingConfigs {
        release {
            storeFile file(credentialsFolder + releaseKeystoreProperties['file'])
            keyAlias releaseKeystoreProperties['alias']
            keyPassword releaseKeystoreProperties['password']
            storePassword releaseKeystoreProperties['keystorePassword']
        }
        debug {
            storeFile file(credentialsFolder + debugKeystoreProperties['file'])
            keyAlias debugKeystoreProperties['alias']
            keyPassword debugKeystoreProperties['password']
            storePassword debugKeystoreProperties['keystorePassword']
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            versionNameSuffix ".${currentBranchCommitCount}"
            shrinkResources true
            minifyEnabled true
            debuggable false
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix ".${currentBranchCommitCount}-debug"
            debuggable true
        }
    }
    productFlavors {
        minSdk15 {
            minSdkVersion 15
        }
        minSdk23 {
            minSdkVersion 23
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    packagingOptions {
        exclude 'META-INF/proguard/androidx-annotations.pro'
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

ext {
    support_library_version = "28.0.0"
    moxy_version = "1.5.3"
    toothpick_version = "1.1.3"
    room_version = "1.1.0"
    fresco_version = "1.9.0"
    frescoimageviewer_version = "0.5.0"
    retrofit_version = "2.4.0"
    junit_version = "4.12"
    mockito_version = "2.8.9"
    mockito_kt_version = "1.5.0"
    leakcanary_version = "1.5.4"
    bottomnavigation_version = "2.1.0"
    adapterdelegates_version = "3.0.1"
    stetho_version = "1.5.0"
    firebase_ads_version = "15.0.1"
    firebase_core_version = "16.0.1"
    firebase_config_version = "16.0.0"
    gson_version = "2.8.5"
    eventbus_version = "3.1.1"
    tray_version = "0.12.0"
}

dependencies {
    implementation(
            //Anko
            "org.jetbrains.anko:anko:$anko_version",
            "org.jetbrains.anko:anko-design:$anko_version",
            "org.jetbrains.anko:anko-recyclerview-v7:$anko_version",
            "org.jetbrains.anko:anko-cardview-v7:$anko_version",
            "org.jetbrains.anko:anko-appcompat-v7:$anko_version",
            "org.jetbrains.anko:anko-support-v4:$anko_version",
            //Support
            "com.android.support:appcompat-v7:$support_library_version",
            "com.android.support:recyclerview-v7:$support_library_version",
            "com.android.support:cardview-v7:$support_library_version",
            "com.android.support:transition:$support_library_version",
            "com.android.support:design:$support_library_version",
            "com.android.support:customtabs:$support_library_version",
            "com.android.support:transition:$support_library_version",
            //Multiline collapsing toolbar
            "net.opacapp:multiline-collapsingtoolbar:27.1.1",
            //MVP Moxy
            "com.arello-mobile:moxy-compiler:$moxy_version",
            "com.arello-mobile:moxy-app-compat:$moxy_version",
            //Image load and cache
            "com.facebook.fresco:fresco:$fresco_version",
            //Photo viewer
            "com.github.stfalcon:frescoimageviewer:$frescoimageviewer_version",
            //Retrofit
            "com.squareup.retrofit2:retrofit:$retrofit_version",
            "com.squareup.retrofit2:converter-gson:$retrofit_version",
            //Database
            "android.arch.persistence.room:runtime:$room_version",
            //Bottom navigation bar
            "com.aurelhubert:ahbottomnavigation:$bottomnavigation_version",
            //DI
            "com.github.stephanenicolas.toothpick:toothpick-runtime:$toothpick_version",
            //Adapter simplify
            "com.hannesdorfmann:adapterdelegates3:$adapterdelegates_version",
            //Gson
            "com.google.code.gson:gson:$gson_version",
            //Firebase
            "com.google.firebase:firebase-core:$firebase_core_version",
            "com.google.firebase:firebase-ads:$firebase_ads_version",
            "com.google.firebase:firebase-config:$firebase_config_version",
            //EventBus
            "org.greenrobot:eventbus:$eventbus_version",
            //Multi-process shared preferences
            "net.grandcentrix.tray:tray:$tray_version",
    )
    implementation(
            //Crash logging
            "com.crashlytics.sdk.android:crashlytics:2.9.2@aar"
    ) { transitive = true }
    debugImplementation(
            "com.squareup.leakcanary:leakcanary-android:$leakcanary_version",
            "com.facebook.stetho:stetho:$stetho_version",
            "com.facebook.stetho:stetho-okhttp3:$stetho_version",
    )
    testImplementation(
            "junit:junit:$junit_version",
            "org.mockito:mockito-core:$mockito_version",
            "com.nhaarman:mockito-kotlin-kt1.1:$mockito_kt_version",
    )
    kapt(
            "com.github.stephanenicolas.toothpick:toothpick-compiler:$toothpick_version",
            "com.arello-mobile:moxy-compiler:$moxy_version",
            "android.arch.persistence.room:compiler:$room_version",
    )
}

configurations.all {
    resolutionStrategy {
        force "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    }
}

apply plugin: 'com.google.gms.google-services'